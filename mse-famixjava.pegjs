// Parse MSE files generated by VerveineJ FamixNG (Famix-Java-Entities)
Root = d:Document ? {return {doc:d}}
Document = OPEN nodes:(ElementNode *) CLOSE {return nodes}
ElementNode = OPEN name:ELEMENTNAME id:Serial ? attrNodes:(AttributeNode *) CLOSE 
	{return {element:name, id:id, attrs:attrNodes}}
Serial = OPEN ID id:INTEGER CLOSE {return id}
AttributeNode = OPEN n:SIMPLENAME vals:(ValueNode *) CLOSE {return {attr:n, vals:vals } }
ValueNode = Primitive / Reference / ElementNode
Primitive = STRING / NUMBER / Boolean
Boolean = TRUE / FALSE
Reference = IntegerReference / NameReference
IntegerReference = OPEN REF i:INTEGER CLOSE {return {ref:i}}
NameReference = OPEN REF e:ELEMENTNAME CLOSE {return {ref:e}}
OPEN = _ "(" _ 
CLOSE = _ ")" _
ID = _ t:"id:" _ {return t}
REF = _ t:"ref:" _ {return t}
TRUE = _ t:"true" _ {return t}
FALSE = _ t:"false" _ {return t} 
ELEMENTNAME "ElementName" = 
	_ "Famix-Java-Entities." elementname:SIMPLENAME _  {return elementname}
SIMPLENAME = _ letter ( letter / digit ) * _  {return text().trim()}
INTEGER = _ digit + _ {return text().trim()}
NUMBER = _ "-" ? digit + ( "." digit + ) ? ( ( "e" / "E" ) ( "-" / "+" ) ? digit + ) ? _ {return text().trim()}
STRING = _ ( "\'" [^'] * "\'" ) + _ {return text().trim()}
letter = [A-Za-z]
digit = [0-9]
_ = space*
space = [ \t\n\r]